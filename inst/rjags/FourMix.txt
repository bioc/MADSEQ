
 model{
for (i in 1:nSNP){
# likelihood
z[i] ~ dbin(theta[i],N[i])
#prior
theta[i] ~ dbeta(a[mixture[i]],b[mixture[i]])
mixture[i] ~ dcat(p[1:Nclust])
p_theta[i] <- p[1]*dbeta(theta[i],a[1],b[1])+p[2]*dbeta(theta[i],a[2],b[2])+p[3]*dbeta(theta[i],a[3],b[3])+p[4]*dbeta(theta[i],a[4],b[4])+p[5]*dbeta(theta[i],a[5],b[5])
log.likelihood[i] <- log(p_theta[i]*dbin(z[i],theta[i],N[i]))
}
# hyperprior
for (clustID in 1:(Nclust-1)){
a[clustID] <- mu[clustID]*kappa
b[clustID] <- (1-mu[clustID])*kappa
}
for (i in 1:nSites){
# likelihood
# coverage for all the other sites
N_cov[i] ~ dnegbin(p_cov,r_cov)
log.cov_likelihood[i] <- log(dnegbin(N_cov[i],p_cov,r_cov))
}
#prior
p_cov <- r_cov/(r_cov+m_cov)
m_cov <- m0*(2+f)/2
r_cov ~ dgamma(0.01,0.01)
f ~ dbeta(1,1)
kappa ~ dgamma(1,0.1)
p[3] ~ dbeta(1,1)I(0,0.495)
p[4] <- p[3]
p[1] <- 0.495-p[4]
p[2] <- 0.495-p[3]
p[5] <- 0.01
a[5] <- 1
b[5] <- 1
d1 <- m*f*(1-m)/(1+m*f)
d2 <- m*f*(1-m)/(1+f-m*f)
d3 <- m*f/(2*(1-m)+m*f)
d4 <- f*(1-m)/(f*(1-m)+2*m)
mu[1] <- m+d1
mu[2] <- m-d2
mu[3] <- 0+d3
mu[4] <- 1-d4  
# report likelihood
aaf.like <- sum(log.likelihood[1:nSNP])
cov.like <- sum(log.cov_likelihood[1:nSites])
sum.like <- aaf.like+cov.like
}
